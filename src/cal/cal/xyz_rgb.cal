{ RCSid $Id: xyz_rgb.cal,v 1.5 2012/03/20 16:24:06 greg Exp $ }
{
	Convert between XYZ and RGB coordinates.

	2/17/95

	Be sure that CIE_x_r, etc. definitions are consistent with
	those in ray/src/common/color.h.
}

CIE_Radiance(i) = select(i, 0.640,0.330, 0.290,0.600, 0.150,0.060, 1/3,1/3);
CIE_Sharp(i) = select(i, 0.6898,0.3206, 0.0736,0.9003, 0.1166,0.0374, 1/3,1/3);
CIE_Adobe(i) = select(i, 0.640,0.330, 0.210,0.710, 0.150,0.060, 0.3127,0.3290);
CIE_RIMM(i) = select(i, 0.7347,0.2653, 0.1596,0.8404, 0.0366,0.0001, 0.3457,0.3585);
CIE_709(i) = select(i, 0.640,0.330, 0.300,0.600, 0.150,0.060, 0.3127,0.3290);
CIE_P3(i) = select(i, 0.680,0.320, 0.265,0.690, 0.150,0.060, 0.314,0.351);

CIE_pri(i) = CIE_Radiance(i);

{*** The whole calculation is based on the CIE (x,y) chromaticities below ***}

CIE_x_r =	CIE_pri(1);
CIE_y_r =	CIE_pri(2);
CIE_x_g =	CIE_pri(3);
CIE_y_g =	CIE_pri(4);
CIE_x_b =	CIE_pri(5);
CIE_y_b =	CIE_pri(6);
CIE_x_w =	CIE_pri(7);
CIE_y_w =	CIE_pri(8);

WHTEFFICACY :	179. ;		{ luminous efficacy of uniform white light }

{ Derived constants }

CIE_D =		CIE_x_r*(CIE_y_g - CIE_y_b) +
				CIE_x_g*(CIE_y_b - CIE_y_r) +
				CIE_x_b*(CIE_y_r - CIE_y_g) ;

CIE_C_rD =	(1./CIE_y_w) *  ( CIE_x_w*(CIE_y_g - CIE_y_b) -
				  CIE_y_w*(CIE_x_g - CIE_x_b) +
				  CIE_x_g*CIE_y_b - CIE_x_b*CIE_y_g	) ;

CIE_C_gD =	(1./CIE_y_w) *  ( CIE_x_w*(CIE_y_b - CIE_y_r) -
				  CIE_y_w*(CIE_x_b - CIE_x_r) -
				  CIE_x_r*CIE_y_b + CIE_x_b*CIE_y_r	) ;

CIE_C_bD =	(1./CIE_y_w) *  ( CIE_x_w*(CIE_y_r - CIE_y_g) -
				  CIE_y_w*(CIE_x_r - CIE_x_g) +
				  CIE_x_r*CIE_y_g - CIE_x_g*CIE_y_r	) ;

{ Convert CIE XYZ coordinates to RGB }

XYZ2RGB(i,j) = select(i*3+j+1,
	(CIE_y_g - CIE_y_b - CIE_x_b*CIE_y_g + CIE_y_b*CIE_x_g)/CIE_C_rD,
	(CIE_x_b - CIE_x_g - CIE_x_b*CIE_y_g + CIE_x_g*CIE_y_b)/CIE_C_rD,
	(CIE_x_g*CIE_y_b - CIE_x_b*CIE_y_g)/CIE_C_rD,
	(CIE_y_b - CIE_y_r - CIE_y_b*CIE_x_r + CIE_y_r*CIE_x_b)/CIE_C_gD,
	(CIE_x_r - CIE_x_b - CIE_x_r*CIE_y_b + CIE_x_b*CIE_y_r)/CIE_C_gD,
	(CIE_x_b*CIE_y_r - CIE_x_r*CIE_y_b)/CIE_C_gD,
	(CIE_y_r - CIE_y_g - CIE_y_r*CIE_x_g + CIE_y_g*CIE_x_r)/CIE_C_bD,
	(CIE_x_g - CIE_x_r - CIE_x_g*CIE_y_r + CIE_x_r*CIE_y_g)/CIE_C_bD,
	(CIE_x_r*CIE_y_g - CIE_x_g*CIE_y_r)/CIE_C_bD
);

noneg(x) : if(x, x, 0);

R(X,Y,Z) =	noneg(XYZ2RGB(0,0)*X + XYZ2RGB(0,1)*Y + XYZ2RGB(0,2)*Z);
G(X,Y,Z) =	noneg(XYZ2RGB(1,0)*X + XYZ2RGB(1,1)*Y + XYZ2RGB(1,2)*Z);
B(X,Y,Z) =	noneg(XYZ2RGB(2,0)*X + XYZ2RGB(2,1)*Y + XYZ2RGB(2,2)*Z);

{ Convert RGB to CIE XYZ coordinates }

RGB2XYZ(i,j) = select(i*3+j+1,
	CIE_x_r*CIE_C_rD/CIE_D,CIE_x_g*CIE_C_gD/CIE_D,CIE_x_b*CIE_C_bD/CIE_D,
	CIE_y_r*CIE_C_rD/CIE_D,CIE_y_g*CIE_C_gD/CIE_D,CIE_y_b*CIE_C_bD/CIE_D,
	(1.-CIE_x_r-CIE_y_r)*CIE_C_rD/CIE_D,
	(1.-CIE_x_g-CIE_y_g)*CIE_C_gD/CIE_D,
	(1.-CIE_x_b-CIE_y_b)*CIE_C_bD/CIE_D
);

X(R,G,B) =	RGB2XYZ(0,0)*R + RGB2XYZ(0,1)*G + RGB2XYZ(0,2)*B;
Y(R,G,B) =	RGB2XYZ(1,0)*R + RGB2XYZ(1,1)*G + RGB2XYZ(1,2)*B;
Z(R,G,B) =	RGB2XYZ(2,0)*R + RGB2XYZ(2,1)*G + RGB2XYZ(2,2)*B;

{ Convert spectral radiance in watts/sr/m^2 to luminance in cd/m^2 }

luminance(r,g,b) = WHTEFFICACY * Y(r,g,b) ;
