#!/bin/csh -f
# RCSid $Id: installib,v 1.3 2003/10/30 15:41:23 greg Exp $
#
# Install library files
#
again2:
echo -n "Where do you want the library files [/usr/local/lib/ray]? "
set ldir=$<
(echo $ldir) >/dev/null
if ( $status ) goto again2
set ldir=$ldir
if ( "$ldir" == "" ) then
	set ldir=/usr/local/lib/ray
else if ( "$ldir" !~ /* ) then
	echo "Directory must be relative to root, please reenter"
	goto again2
endif
if ( ! -d $ldir ) then
	mkdir $ldir
	if ( $status ) then
		echo "Cannot create directory, please reenter"
		goto again2
	endif
endif
if (! -d lib) then
	echo ">>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>"
	echo "You forgot to install the auxiliary files overlay."
	echo "Download rad3R6supp.tar.gz from http://www.radiance-online.org"
	echo "and run 'installib' later manually, or ^C now."
	echo "<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<"
	exit
endif
set d1=(`ls -Lid lib`)
set d2=(`ls -Lid $ldir`)
if ($d1[1] != $d2[1]) then
	echo -n "Install library files now [n]? "
	if ( "$<" =~ [yY]* ) then
		echo -n "Copying library files to $ldir... "
		(cd lib ; tar -cf - *) | (cd $ldir ; tar -xf -)
		echo "Done."
	endif
endif
unset d1 d2
